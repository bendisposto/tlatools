apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

project.version = '1.0.3-SNAPSHOT'
project.group = 'de.hhu.stups'
project.archivesBaseName = 'tlatools'

project.sourceCompatibility = JavaVersion.VERSION_1_8
project.targetCompatibility = JavaVersion.VERSION_1_8

repositories {
	mavenCentral()
}

dependencies {
	implementation(files('lib/javax.mail.jar'))
	testImplementation(files('lib/easymock-3.3.1.jar', 'lib/jpf.jar', 'lib/junit-4.12.jar', 'lib/javax.mail.jar'))
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'src'
		}
	}
}

javadoc {
	failOnError = false
}

task tlatools(type: Copy, dependsOn: build) {
	from('build/libs/')
	into('build/tlatools')
	include('tlatools-' + project.version + '.jar')
	rename('tlatools-(.+)', 'tlatools.jar')
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
	println "Configuring deployment for ${ project.name }"

	apply plugin: 'signing'

	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
		// Ignore duplicates because the same directory is used for sources and resources.
		duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	}

	artifacts {
		archives javadocJar, sourcesJar
	}

	ext."signing.secretKeyRingFile" = rootProject.file("secring.gpg").absolutePath

	signing {
		sign configurations.archives
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				pom.project {
					name "TLA+ Tools"
					packaging 'jar'
					// optionally artifactId can be defined here
					description 'Modified version of the TLA+ tools for usage in ProB'
					url 'https://github.com/hhu-stups/tlatools'

					licenses {
						license {
							name 'MIT License'
							url 'http://research.microsoft.com/en-us/um/people/lamport/tla/license.html'
						}
					}

					scm {
						connection 'scm:git:git://github.com/hhu-stups/tlatools.git'
						developerConnection 'scm:git:git@github.com:hhu-stups/tlatools.git'
						url 'https://github.com/hhu-stups/tlatools'
					}

					developers {
						developer {
							id 'bendisposto'
							name 'Jens Bendisposto'
							email 'jens@bendisposto.de'
						}
					}
				}
			}
		}
	}
}
