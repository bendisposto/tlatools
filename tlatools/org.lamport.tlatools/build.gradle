apply plugin: 'java'
apply plugin: "maven-publish"
apply plugin: "signing"

project.version = '1.1.0-SNAPSHOT'
project.group = 'de.hhu.stups'

final isSnapshot = project.version.endsWith("-SNAPSHOT")

repositories {
	mavenCentral()
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'src'
		}
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8

	withSourcesJar()
	withJavadocJar()
}

javadoc {
	// The TLA Tools source code uses unescaped HTML symbols in Javadoc text,
	// which is considered an error.
	failOnError = false
}

sourcesJar {
	// Ignore duplicates because the same directory is used for sources and resources.
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task tlatools(type: Copy, dependsOn: build) {
	from('build/libs/')
	into('build/tlatools')
	include('tlatools-' + project.version + '.jar')
	rename('tlatools-(.+)', 'tlatools.jar')
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			pom {
				name = "TLA+ Tools"
				description = "Modified version of the TLA+ tools for usage in ProB"
				url = "https://github.com/hhu-stups/tlatools"

				licenses {
					license {
						name = "MIT License"
						url = "http://research.microsoft.com/en-us/um/people/lamport/tla/license.html"
					}
				}

				scm {
					connection = "scm:git:git://github.com/hhu-stups/tlatools.git"
					developerConnection = "scm:git:git@github.com:hhu-stups/tlatools.git"
					url = "https://github.com/hhu-stups/tlatools"
				}

				developers {
					developer {
						id = "bendisposto"
						name = "Jens Bendisposto"
						email = "jens@bendisposto.de"
					}
				}
			}
		}
	}

	repositories {
		maven {
			final releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			final snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
			url isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
			if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
				credentials {
					username project.ossrhUsername
					password project.ossrhPassword
				}
			}
		}
	}
}

ext."signing.secretKeyRingFile" = rootProject.file("secring.gpg").absolutePath

signing {
	sign publishing.publications.mavenJava
}
